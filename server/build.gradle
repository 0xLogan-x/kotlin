group 'server'
version '1.0'

buildscript {
    ext.kotlin_version = '1.0.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.5

repositories {
    mavenCentral()
}

task compileProto(type: Exec) {
    /*
     * XXX Kotlin proto compiler calling convention will change soon.
     * It would provide fixed path to protoc in its project directory.
     */
    executable "./compileProto.sh"
}
task buildProtoLib(type: Copy) {
    /*
     * XXX Will need to rework.
     * New Kotlin protobuf compiler will have build.sh script that
     * generates jar file with Kotlin protobuf runtime classes
     * and .proto => .kt compiler executable.
     * Also you copy this files into src of your server and even
     * commit copied files into git repository.
     * This is not acceptable. This files shouldn't be part of
     * your src in any way.
     * Also Kotlin protoc generated files shouldn't be part of your src too.
     */
    //just copy lib src to project
    def pathToProtoKotSrc = "../proto/compiler/src/"
    from pathToProtoKotSrc + "CodedInputStream.kt", pathToProtoKotSrc + "CodedOutputStream.kt",
            pathToProtoKotSrc + "WireFormat.kt", pathToProtoKotSrc + "WireType.kt",
            pathToProtoKotSrc + "InvalidProtocolBufferException.kt"
    into "src/main/java"
}

task getDeps(type: Copy) {
    from sourceSets.main.compileClasspath
    into 'build/libs'
}

build.dependsOn getDeps
compileKotlin.dependsOn compileProto
compileProto.dependsOn buildProtoLib

jar {
    manifest {
        attributes("Implementation-Title": "Gradle",
                "Implementation-Version": version,
                "Class-Path": "kotlin-runtime-1.0.3.jar kotlin-stdlib-1.0.3.jar netty-all-4.1.2.Final.jar",
                "Main-Class": "ServerMainKt")
    }

}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "io.netty:netty-all:4.1.2.Final"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
